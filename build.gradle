buildscript {
	ext {
		springBootVersion = '2.5.6'
	}
	repositories {
		mavenCentral()
		gradlePluginPortal()
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath("com.github.node-gradle:gradle-node-plugin:3.1.1")
		classpath "org.openapitools:openapi-generator-gradle-plugin:5.3.0"
		classpath "gradle.plugin.com.google.cloud.tools:jib-gradle-plugin:3.2.1"
}
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.github.node-gradle.node'
apply plugin: 'org.openapi.generator'
apply plugin: "com.google.cloud.tools.jib"



group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.junit.jupiter:junit-jupiter:5.7.0'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-validation'
	testImplementation 'junit:junit:4.12'
}

task copyWebApp(type: Copy) {
	from 'src/main/webapp/build'
	into 'build/resources/main/static/.'
}

task appNpmInstall(type: NpmTask) {
	description = "Installs all dependencies from package.json"
	workingDir = file("${project.projectDir}/src/main/webapp")
	args = ["install"]
}

task appNpmBuild(type: NpmTask) {
	description = "Builds production version of the webapp"
	workingDir = file("${project.projectDir}/src/main/webapp")
	args = ["run", "build"]
}

appNpmBuild.dependsOn appNpmInstall
copyWebApp.dependsOn appNpmBuild
compileJava.dependsOn copyWebApp

node {
	download = true

	// Set the work directory for unpacking node
	workDir = file("${project.buildDir}/nodejs")

	// Set the work directory for NPM
	npmWorkDir = file("${project.buildDir}/npm")

	//workDir = file("${project.buildDir}/node")
	nodeModulesDir = file("${project.projectDir}/src/main/webapp")
	nodeProjectDir =file("${project.projectDir}/src/main/webapp")
}